-- the readings: the gauges of the house, read from a grown stream by the kernel — reasons, roots,
-- census, kinds, the schema. none of them is an input to a trial; they live apart from bin/judge.lean
-- (the trial, and the three readings that feed it: needs, cites, templates) so that the memo's key,
-- which folds the judge, is not moved by a gauge (2026-09-30: a roots verb added to the judge sent the
-- house cold, locally and in CI, for a reading no trial could hear)
import Lean
import Pieces
open Lean Elab

def elabFrom (src : String) (name : String) (st : Option Command.State) (extra : Array Import := #[]) : IO Command.State := do
  let ictx := Parser.mkInputContext src name
  let (hdr, ps, msgs) ← Parser.parseHeader ictx
  let cs ← match st with
    | some s => pure { s with messages := {} }
    | none =>
        let imports := headerToImports hdr ++ extra
        let env ← importModules (if imports.isEmpty then #[{ module := `Init }] else imports) {} 0 (loadExts := true)
        pure (Command.mkState env msgs (({} : Options).setBool `Elab.async false))
  let s ← IO.processCommands ictx ps cs
  return s.commandState


/-- the namespaces a reading is about: the trail's own, and the imported ones whose
theorems the trail may cite (given as `ns` and a comma-separated `imports` arg) -/
structure Scope where
  ns : Name
  imported : List Name

def Scope.parse (args : List String) (i : Nat) : Scope :=
  let ns := (args[i]?).map String.toName |>.getD `Foam
  let imported := ((args[i+1]?).getD "").splitOn "," |>.filter (· ≠ "") |>.map String.toName
  ⟨ns, imported⟩

def Scope.owns (sc : Scope) (n : Name) : Bool :=
  n.getPrefix == sc.ns || sc.imported.any (fun m => n.getPrefix == m)

partial def usedTopLevel (env : Environment) (sc : Scope) (seen : NameSet) (n : Name) : NameSet := Id.run do
  let some ci := env.find? n | return seen
  let mut seen := seen
  let consts := ci.type.getUsedConstants ++ (match ci.value? (allowOpaque := true) with | some v => v.getUsedConstants | none => #[])
  for c in consts do
    if seen.contains c then continue
    if sc.owns c then
      seen := seen.insert c
    else if (env.getModuleIdxFor? c).isNone then
      seen := usedTopLevel env sc (seen.insert c) c
  return seen

def nameOf (sc : Scope) (n : Name) : String :=
  if n.getPrefix == sc.ns then n.getString! else n.toString


/-- a carrier's body may live in an auxiliary named under it (`backed._f` on Lean 4.31, a matcher, a
`_unary`): the unfolding follows every auxiliary of the carrier and stops at any other name -/
partial def reasonsUnfold (env : Environment) (houseWord : Name → Bool) (c : Name) (out : Array Name) (fuel : Nat) : Array Name := Id.run do
  if fuel == 0 then return out
  let some cci := env.find? c | return out
  if cci.isTheorem then return out
  let some v := cci.value? | return out
  let mut out := out
  for d in v.getUsedConstants do
    if houseWord d && !out.contains d then out := out.push d
    if d.getPrefix == c || d.getPrefix.getPrefix == c then out := reasonsUnfold env houseWord d out (fuel - 1)
  return out

/-- reasons: every citation of the trail, with how many words of the house the two statements share —
none shared, while the cited statement has some, is a citation by computation alone (the terms unify
after unfolding, and the name explains nothing to a reader of names); the chart draws such an arrow
dashed and the book counts them. a cited statement with no house word (`0 + n = n`) is core grammar -/
def reasonsMode (trail : String) (sc : Scope) : IO Unit := do
  let st ← elabFrom (← IO.FS.readFile trail) trail none
  let env := st.env
  -- a word of the house: a constant of no module of Lean's own (the trail's, or any grown module)
  let houseWord (c : Name) : Bool := match env.getModuleIdxFor? c with
    | none => true
    | some idx => let r := env.header.moduleNames[idx.toNat]!.getRoot; !(r == `Init || r == `Lean || r == `Std || r == `Lake)
  -- a statement's words, with one unfolding through the house's own carriers (a statement about
  -- `perms` speaks of `joinMap`): the same reading the search takes of a goal (Pieces.vocab)
  let wordsOf (t : Expr) : Array Name := Id.run do
    let mut out : Array Name := #[]
    for c in t.getUsedConstants do
      if !houseWord c then continue
      out := out.push c
      out := reasonsUnfold env houseWord c out 4
    return out
  for (n, ci) in env.constants.map₂.toList do
    if n.getPrefix != sc.ns || !ci.isTheorem then continue
    let used := usedTopLevel env sc {} n
    let words := wordsOf ci.type
    for d in used.toList do
      if d == n || !(sc.owns d) then continue
      let some di := env.find? d | continue
      if !di.isTheorem then continue
      let own := wordsOf di.type
      let shared := (own.filter (fun c => words.contains c)).size
      IO.println s!"{n.getString!} {nameOf sc d} {shared} {own.size}"

/-- the roots, read from the shadow: S is an INSTANCE of T when S's statement is T's with T's binders
filled — unification of the two statements, no body and no ranking in it; an Eq is tried both ways
round. a theorem many instantiate and that instantiates none is a sink: a root by the statements alone.
prints `S T` per instance found; the trail's own theorems as S, the trail's and its imports' as T -/
def rootsMode (trail : String) (sc : Scope) (byShape : Bool) : IO Unit := do
  let st ← elabFrom (← IO.FS.readFile trail) trail none
  let env := st.env
  let mut thms : Array Name := #[]
  for (n, ci) in env.constants.map₂.toList do
    if n.getPrefix == sc.ns && ci.isTheorem then thms := thms.push n
  let mut targets : Array Name := thms
  for (n, ci) in env.constants.map₁.toList do
    if sc.owns n && n.getPrefix != sc.ns && ci.isTheorem then targets := targets.push n
  let res ← (Meta.MetaM.toIO (ctxCore := { fileName := trail, fileMap := default, maxHeartbeats := 5000 * 1000 }) (sCore := { env := env }) do
    let mut out : Array (Name × Name) := #[]
    for s in thms do
      let some si := env.find? s | continue
      for t in targets do
        if t == s then continue
        let hb ← IO.getNumHeartbeats
        let ok ← try
          withTheReader Core.Context (fun c => { c with initHeartbeats := hb }) do
            (if byShape then Meta.withReducible else id) <| Meta.withNewMCtxDepth do
              Meta.forallTelescope si.type fun sFvars sBody => do
                let tc ← Meta.mkConstWithFreshMVarLevels t
                let tType ← Meta.inferType tc
                let (mvs, _, tBody) ← Meta.forallMetaTelescope tType
                -- the conclusion, by unification (an Eq either way round)
                let hit ← (do
                  if ← Meta.isDefEq sBody tBody then return true
                  match tBody.eq?, sBody.eq? with
                  | some (α, a, b), some _ => Meta.isDefEq sBody (mkApp3 (mkConst ``Eq [← Meta.getLevel α]) α b a)
                  | _, _ => return false)
                if !hit then return false
                -- every binder of T filled: by the conclusion, or by a hypothesis of S of the same type
                for m in mvs do
                  if ← m.mvarId!.isAssigned then continue
                  let mty ← instantiateMVars (← Meta.inferType m)
                  let mut done := false
                  for f in sFvars do
                    if done then continue
                    if ← Meta.isDefEq mty (← Meta.inferType f) then
                      if ← Meta.isDefEq m f then done := true
                  if !done then return false
                return true
        catch _ => pure false
        if ok then out := out.push (s, t)
    return out)
  for (s, t) in res.1 do
    IO.println s!"{s.getString!} {nameOf sc t}"

partial def stripLams : Expr → Expr
  | .lam _ _ b _ => stripLams b
  | e => e

partial def usedAll (env : Environment) (ns : Name) (seen : NameSet) (n : Name) : NameSet := Id.run do
  let some ci := env.find? n | return seen
  let mut seen := seen
  let consts := ci.type.getUsedConstants ++ (match ci.value? (allowOpaque := true) with | some v => v.getUsedConstants | none => #[])
  for c in consts do
    if seen.contains c then continue
    seen := seen.insert c
    if (env.getModuleIdxFor? c).isNone && c.getPrefix != ns then
      seen := usedAll env ns seen c
  return seen

def censusMode (trail : String) (sc : Scope) : IO Unit := do
  let st ← elabFrom (← IO.FS.readFile trail) trail none
  let env := st.env
  let ns := sc.ns
  let reflHeads : List Name := [`Eq.refl, `rfl, `Iff.refl, `Iff.rfl, `HEq.refl, `HEq.rfl]
  for (n, ci) in env.constants.map₂.toList do
    if n.getPrefix != ns || !ci.isTheorem then continue
    let some v := ci.value? (allowOpaque := true) | continue
    let body := stripLams v
    let head := body.getAppFn.constName?
    let isRefl := head.any reflHeads.contains
    let used := usedAll env ns {} n
    let names := used.toList
    let isRec := names.any (fun c =>
      let s := c.getString!
      (s == "brecOn" || s == "rec" || s == "recOn") &&
        !(c.getPrefix == `Eq || c.getPrefix == `HEq || c.getPrefix == `Acc || c.getPrefix == `WellFounded))
    let isCases := names.any (fun c =>
      let s := c.getString!
      s == "casesOn" || s.startsWith "match_")
    let cites := names.any (fun c => c != n && c.getPrefix == ns && (env.find? c).any (·.isTheorem))
    let cls := if isRefl then "rfl" else if isRec then "induction" else if isCases then "cases" else if cites then "citation" else "term"
    IO.println s!"{n.getString!} {cls}"

partial def codomain : Expr → Expr
  | .forallE _ _ b _ => codomain b
  | e => e

/-- kinds: every carrier (non-theorem) of the namespace with the sort of its codomain —
`prop` (a relation; no `#guard` row can decide it, its theorems are its treaty), `sort`
(a type; rows evaluate its inhabitants, never the name), or `data` (a row can compute it) -/
def kindsMode (trail : String) (sc : Scope) : IO Unit := do
  let st ← elabFrom (← IO.FS.readFile trail) trail none
  let env := st.env
  for (n, ci) in env.constants.map₂.toList do
    if n.getPrefix != sc.ns || ci.isTheorem then continue
    let k := match codomain ci.type with
      | .sort l => if l == Level.zero then "prop" else "sort"
      | _ => "data"
    IO.println s!"{n.getString!} {k}"


/-- the component of a tuple a projection chain reads: `x.1` is 0, `x.2.1` is 1, a bare `x.2.2` is
the last (`snd` all the way down ends in the last component) -/
partial def compDepth (b : Expr) (k : Nat) : Option Nat :=
  if b.isAppOfArity ``Prod.snd 3 then compDepth (b.getArg! 2) (k + 1)
  else if b.isAppOfArity ``Prod.fst 3 then some k
  else if b.isBVar then some k
  else none

/-- a literal list's elements, as constant names -/
partial def listItems (e : Expr) : Option (List Name) :=
  if e.isAppOfArity ``List.nil 1 then some []
  else if e.isAppOfArity ``List.cons 3 then
    match (e.getArg! 1).getAppFn.constName?, listItems (e.getArg! 2) with
    | some c, some rest => some (c :: rest) | _, _ => none
  else none

/-- an enum, by name -/
def isEnumName (env : Environment) (n : Name) : Bool :=
  match env.find? n with
  | some (.inductInfo ii) => !ii.isRec && ii.ctors.all fun c => match env.find? c with
    | some (.ctorInfo ci) => ci.numFields == 0 | _ => false
  | _ => false

/-- an enum's SQL type: its short name in snake case -/
def enumSQL (n : Name) : String := Id.run do
  let mut out := ""
  for c in n.getString!.toList do
    if c.isUpper then out := out ++ (if out.isEmpty then "" else "_") ++ toString c.toLower else out := out.push c
  return out

/-- a literal list: the cons chain to its nil, with the element type -/
partial def listLiteral? (e : Expr) : Option (List Expr × Expr) :=
  if e.isAppOfArity ``List.nil 1 then some ([], e.getArg! 0)
  else if e.isAppOfArity ``List.cons 3 then
    (listLiteral? (e.getArg! 2)).map fun (xs, t) => (e.getArg! 1 :: xs, t)
  else none

/-- a column name Postgres would refuse bare gets a trailing underscore; the drawer and the
translator apply the same rule -/
def reservedSQL : List String := "user table order group from select where to end check default primary references in is on all and or not as by into join case when then else null limit offset union values with using cast column constraint create do for grant having only some window distinct desc asc any array between except fetch foreign intersect lateral returning unique".splitOn " "
def colSQL (f : String) : String := if reservedSQL.contains f then f ++ "_" else f
def tableSQL (n : Name) : String := colSQL (enumSQL n)

/-- a structure the shadow keeps as a table: none of its fields is a type (a Face is a kind) -/
def isTableStruct (env : Environment) (sn : Name) : Bool :=
  isStructure env sn && (getStructureFields env sn).all fun f =>
    match env.find? (sn ++ f) with
    | some pi => Id.run do
      let mut t := pi.type
      while t.isForall do t := t.bindingBody!
      return !t.isSort
    | none => false

/-- a fragment of the list vocabulary, read into SQL: projections are columns, `cons` prepends,
a house def is a call, `And`/`Eq`/`∈` are themselves, `cond` is `CASE`, and the joinMap-over-a-cond
shape is an aggregate over the child rows; anything outside the fragment is marked unread -/
partial def toSQL (env : Environment) (self : Nat) (e : Expr) : String :=
  let go := toSQL env self
  let col (c : Name) := colSQL c.getString!
  -- the element type of an array, for an empty one
  let elemSQL (t : Expr) : String := match t.constName? with
    | some ``Nat => "integer" | some ``Bool => "boolean"
    | some c => if isEnumName env c then enumSQL c else "integer"
    | none => "integer"
  -- a predicate applied to the element `x` of an array
  let pred (p : Expr) : String := match p with
    | .lam _ _ body _ => toSQL env self (body.instantiate1 (.const `x []))
    | _ => go (mkApp p (.const `x []))
  -- a chain of pair projections is a component: `.1` is c0, `.2.1` c1, `.2.2` c2
  let rec comp (e : Expr) (k : Nat) : Option (Nat × Expr) :=
    if e.isAppOfArity ``Prod.fst 3 then some (k, e.getArg! 2)
    else if e.isAppOfArity ``Prod.snd 3 then
      match comp (e.getArg! 2) (k + 1) with
      | some r => some r
      | none => some (k + 1, e.getArg! 2)
    else match e with
      | .proj ``Prod 0 b => some (k, b)
      | .proj ``Prod 1 b => (match comp b (k + 1) with | some r => some r | none => some (k + 1, b))
      | _ => none
  match comp e 0 with
  | some (k, base) => s!"({go base}).c{k}"
  | none =>
  match e with
  | .bvar i => if i == self then "row" else s!"${i}"
  | .const ``Bool.true _ => "true"
  | .const ``Bool.false _ => "false"
  | .const ``List.nil _ => "ARRAY[]::integer[]"
  | .const `x _ => "x"
  | .const c _ =>
    -- a constructor of an enum is its literal; a marker (`row`, `p1`) prints by name
    match env.find? c with
    | some (.ctorInfo ci) => if isEnumName env ci.induct then s!"'{c.getString!}'" else s!"⟨unread {c}⟩"
    | none => if c.getPrefix == .anonymous then c.toString else s!"⟨unread {c}⟩"
    | _ => s!"⟨unread {c}⟩"
  | .app .. =>
    let f := e.getAppFn; let args := e.getAppArgs
    match listLiteral? e with
    | some ([], t) => s!"ARRAY[]::{elemSQL t}[]"
    | some (xs, t) => s!"ARRAY[{", ".intercalate (xs.map go)}]::{elemSQL t}[]"
    | none =>
    match f.constName?, args.size with
    | some ``And, 2 => s!"({go args[0]!} AND {go args[1]!})"
    | some ``Bool.and, 2 => s!"({go args[0]!} AND {go args[1]!})"
    | some ``Bool.or, 2 => s!"({go args[0]!} OR {go args[1]!})"
    | some ``Bool.not, 1 => s!"(NOT {go args[0]!})"
    | some ``Eq, 3 => s!"({go args[1]!} = {go args[2]!})"
    -- a membership through a subquery, so any element type reads and a subselect is not the row form
    | some ``Membership.mem, 5 => s!"({go args[4]!} = ANY(SELECT unnest({go args[3]!})))"
    | some ``List.cons, 3 => s!"array_prepend({go args[1]!}, {go args[2]!})"
    | some ``cond, 4 => s!"(CASE WHEN {go args[1]!} THEN {go args[2]!} ELSE {go args[3]!} END)"
    | some ``List.length, 2 => s!"cardinality({go args[1]!})"
    -- over rows (a list of pairs is a child table's rows, kept whole): the alias is the row, the
    -- order its own position; over scalars: the element with its ordinality
    | some ``List.filter, 3 =>
      if args[0]!.isAppOf ``Prod then s!"ARRAY(SELECT x FROM unnest({go args[2]!}) AS x WHERE {pred args[1]!} ORDER BY (x).position)"
      else s!"ARRAY(SELECT x FROM unnest({go args[2]!}) WITH ORDINALITY AS u(x, o) WHERE {pred args[1]!} ORDER BY o)"
    | some ``List.map, 4 =>
      if args[0]!.isAppOf ``Prod then s!"ARRAY(SELECT {pred args[2]!} FROM unnest({go args[3]!}) AS x ORDER BY (x).position)"
      else s!"ARRAY(SELECT {pred args[2]!} FROM unnest({go args[3]!}) WITH ORDINALITY AS u(x, o) ORDER BY o)"
    | some ``List.sum, 4 => s!"(SELECT coalesce(sum(x), 0) FROM unnest({go args[3]!}) AS x)"
    -- a face applied to a row at a probe: a column of the face's function (the drawer names the table)
    | some `Face.Face.obs, 3 =>
      let F := args[0]!
      match F.getAppFn.constName? with
      | some fn => s!"FACEOBS[{fn.getString!}]({", ".intercalate (F.getAppArgs.toList.map go)})({go args[1]!}; {go args[2]!})"
      | none => s!"⟨unread Face.obs⟩"
    | some ``OfNat.ofNat, 3 => go args[1]!
    | some ``Nat.beq, 2 => s!"({go args[0]!} = {go args[1]!})"
    | some ``Nat.ble, 2 => s!"({go args[0]!} <= {go args[1]!})"
    | some ``HAdd.hAdd, 6 => s!"({go args[4]!} + {go args[5]!})"
    | some ``HMod.hMod, 6 => s!"({go args[4]!} % {go args[5]!})"
    -- the weight: how many of the needs are not enrolled
    | some `Room.lacking, 4 => s!"cardinality(ARRAY(SELECT x FROM unnest({go args[3]!}) WITH ORDINALITY AS u(x, o) WHERE NOT (x = ANY(SELECT unnest({go args[2]!}))) ORDER BY o))"
    -- the first voice: whichever of a, b the list names first — a, true; b, false; neither, false
    | some `Room.firstOf, 5 => s!"coalesce((SELECT (x = {go args[2]!}) FROM unnest({go args[4]!}) WITH ORDINALITY AS u(x, o) WHERE (x = {go args[2]!}) OR (x = {go args[3]!}) ORDER BY o LIMIT 1), false)"
    -- a fold: a machine parked over a list is its step run on the row for each element in order,
    -- when the step is a house clerk by name
    | some `Face.park, 5 =>
      match args[2]!.constName? with
      | some mn =>
        match env.find? mn with
        | some ci =>
          match ci.value? with
          | some v =>
            let stepE := if v.isAppOfArity `Face.Machine.mk 6 then (v.getAppArgs)[4]! else v
            match stepE.constName? with
            | some f => s!"FOLD[{f.getString!}]({go args[3]!}; {go args[4]!})"
            | none => s!"⟨unread Face.park⟩"
          | none => s!"⟨unread Face.park⟩"
        | none => s!"⟨unread Face.park⟩"
      | none => s!"⟨unread Face.park⟩"
    | some `Room.everyone, 3 => s!"({go args[1]!} <@ {go args[2]!})"   -- every member enrolled: containment
    | some `Room.backed, 4 => s!"({go args[3]!} <@ {go args[2]!})"
    | some `Room.enrolled, 4 => s!"({go args[3]!} = ANY(SELECT unnest({go args[2]!})))"
    | some `Room.joinMap, n =>
      -- joinMap (fun p => cond p.q p.f []) xs: the rows of xs where q, their f's flattened in order;
      -- with xs left implicit (a bare `joinMap f`), the rows are the argument itself
      if n < 3 then s!"⟨unread joinMap⟩" else
      match args[2]! with
      | .lam _ _ (.app (.app (.app (.app (.const ``cond _) _) q) f) (.app (.const ``List.nil _) _)) _ =>
        s!"AGG[{toSQL env 0 q} → {toSQL env 0 f}]({if n ≥ 4 then go args[3]! else "row"})"
      -- a joinMap over any other lambda: each element's list, flattened in order
      | .lam .. => if n ≥ 4 then s!"ARRAY(SELECT m FROM unnest({go args[3]!}) WITH ORDINALITY AS u(x, o), unnest({pred args[2]!}) WITH ORDINALITY AS v(m, i) ORDER BY o, i)" else s!"⟨unread joinMap⟩"
      | _ => s!"⟨unread joinMap⟩"
    | some c, _ =>
      if isStructure env c.getPrefix && (getStructureFields env c.getPrefix).contains c.getString!.toName then
        -- a projection: `row.field`; on anything but the row (an argument, an element), a lookup
        -- of the field on the structure's table by id — a pair keeps its component
        let base := args[args.size - 1]!
        if base == .bvar self || base == .const `row [] || c.getPrefix == ``Prod then s!"{go base}.{col c}"
        else if isTableStruct env c.getPrefix then s!"(SELECT {col c} FROM {tableSQL c.getPrefix} WHERE id = {go base})"
        else s!"⟨unread {c}⟩"
      else if c.getString! == "beq" && isEnumName env c.getPrefix && args.size == 2 then
        s!"({go args[0]!} = {go args[1]!})"
      else if c.getPrefix == `Room || c.getPrefix == `Roster || c.getPrefix.getString! == "Treaty" then
        -- a house call; a bare function argument (a comparator) is not a column
        let isFn (a : Expr) := match a.constName? with
          | some cn => match env.find? cn with | some ci => ci.type.isForall | none => false
          | none => false
        s!"{c.getString!}({", ".intercalate (args.toList.filter (fun a => !(isFn a || a.isLambda)) |>.map go)})"
      else s!"⟨unread {c}⟩"
    | none, _ => s!"⟨unread⟩"
  | .proj sn i b => match (getStructureFields env sn)[i]? with
    | some f => if b == .bvar self || b == .const `row [] || sn == ``Prod then s!"{go b}.{colSQL f.getString!}" else if isTableStruct env sn then s!"(SELECT {colSQL f.getString!} FROM {tableSQL sn} WHERE id = {go b})" else "⟨unread⟩"
    | none => "⟨unread⟩"
  | .lit (.natVal n) => toString n
  | _ => "⟨unread⟩"

/-- the data-model shadow, read from the kernel: every structure of the stream and of its house
imports that the stream's vocabulary names (`table`), every enum (`type`), every seat — a def
whose value is a literal list of an enum's constructors (`seat`), and every reader — a def
`State → Probe → Ans` — with the field each probe lands on, found by reducing the reader at that
probe and taking the projection at its head (`reader`); and which theorems cite which seats -/
def schemaMode (trail : String) (sc : Scope) : IO Unit := do
  let st ← elabFrom (← IO.FS.readFile trail) trail none
  let env := st.env
  let mut vocab : Array Name := #[]
  for (n, ci) in env.constants.map₂.toList do
    if n.getPrefix != sc.ns then continue
    vocab := vocab ++ ci.type.getUsedConstants
    if let some v := ci.value? (allowOpaque := true) then vocab := vocab ++ v.getUsedConstants
  let isEnum (ii : InductiveVal) : Bool := !ii.isRec && ii.ctors.all fun c => match env.find? c with
    | some (.ctorInfo ci) => ci.numFields == 0 | _ => false
  let mut seen : Array Name := #[]
  for c in vocab ++ (env.constants.map₂.toList.map (·.1)).toArray do
    if seen.contains c || !sc.owns c then continue
    seen := seen.push c
    match env.find? c with
    | some (.inductInfo ii) =>
      if isEnum ii then IO.println s!"type {c} {" ".intercalate (ii.ctors.map (·.getString!))}"
      else if isStructure env c then
        let mut cols : Array String := #[]
        let mut data := true
        for f in getStructureFields env c do
          if let some pi := env.find? (c ++ f) then
            -- the field's type is the projection's codomain, after its self binder
            let mut t := pi.type
            while t.isForall do t := t.bindingBody!
            if t.isSort then data := false
            cols := cols.push s!"{f}:{t}"
        -- a structure with a Type-valued field (a Face) is a kind, not a table
        if data then IO.println s!"table {c} {" ".intercalate cols.toList}"
    | _ => pure ()
  for (n, ci) in env.constants.map₂.toList do
    if n.getPrefix != sc.ns || ci.isTheorem then continue
    let some v := ci.value? | continue
    match listItems v with
    | some (c :: cs) =>
      if let some (.ctorInfo ci') := env.find? c then
        if let some (.inductInfo ii) := env.find? ci'.induct then
          if isEnum ii then IO.println s!"seat {n.getString!} {ci'.induct} {" ".intercalate ((c :: cs).map (·.getString!))}"
    | _ => pure ()
  for (n, ci) in env.constants.map₂.toList do
    if n.getPrefix != sc.ns || ci.isTheorem then continue
    let ty := ci.type
    if !ty.isForall then continue
    let stateTy := ty.bindingDomain!
    let rest := ty.bindingBody!
    if !rest.isForall then continue
    let probeTy := rest.bindingDomain!
    let some (.inductInfo pii) := env.find? (probeTy.getAppFn.constName?.getD .anonymous) | continue
    if !isEnum pii then continue
    let some stateName := stateTy.getAppFn.constName? | continue
    if !isStructure env stateName then continue
    let fields := getStructureFields env stateName
    let arms ← (Meta.MetaM.toIO (ctxCore := { fileName := trail, fileMap := default }) (sCore := { env := env }) do
      Meta.withLocalDeclD `r stateTy fun r => do
        let mut arms : Array String := #[]
        for c in pii.ctors do
          let e ← Meta.whnf (mkAppN (.const n []) #[r, .const c []])
          -- a direct arm reduces to a projection NODE (`r.3`), an arm under a map keeps the
          -- projection FUNCTION (`Room.guests r`); read both
          let mut hit : Option Name := none
          for i in [0:fields.size] do
            let f := fields[i]!
            if (e.find? fun s => s.isAppOf (stateName ++ f) || (match s with | .proj sn idx _ => sn == stateName && idx == i | _ => false)).isSome then
              hit := some f; break
          -- an arm `List.map (fun x => x.2.2) (field r)` reads one component of each element: name it
          let comp : String := match e with
            | .app (.app (.app (.app (.const ``List.map _) _) _) (.lam _ _ body _)) _ =>
              match compDepth body 0 with | some k => s!"#{k}" | none => ""
            | _ => ""
          if let some f := hit then arms := arms.push s!"{c.getString!}={f}{comp}"
        pure arms)
    if !arms.1.isEmpty then IO.println s!"reader {n.getString!} {stateName} {pii.name} {" ".intercalate arms.1.toList}"
  -- faces: a def whose type, after its parameters, is a Face over a table with an enum (or Unit)
  -- probe — each probe's reading reduced at the probe, the parameters as p1…, the row as `row`
  for (n, ci) in env.constants.map₂.toList do
    if n.getPrefix != sc.ns || ci.isTheorem then continue
    let mut ty := ci.type
    while ty.isForall do ty := ty.bindingBody!
    if ty.getAppFn.constName? != some `Face.Face then continue
    let res ← (Meta.MetaM.toIO (ctxCore := { fileName := trail, fileMap := default }) (sCore := { env := env }) do
      Meta.forallTelescope ci.type fun ps _ => do
        let F := mkAppN (.const n []) ps
        let stateTy ← Meta.whnf (← Meta.mkProjection F `State)
        let probeTy ← Meta.whnf (← Meta.mkProjection F `Probe)
        let ansTy ← Meta.whnf (← Meta.mkProjection F `Ans)
        let some stateName := stateTy.getAppFn.constName? | return none
        if !isTableStruct env stateName then return none
        let probeName := probeTy.getAppFn.constName?.getD .anonymous
        if probeName == ``Nat then
          -- a face whose probes are names: its reading is one function of the row and the probe
          let obs ← Meta.mkProjection F `obs
          let markers := ps.mapIdx fun i _ => Expr.const (Name.mkSimple s!"p{i + 1}") []
          let arm ← Meta.withLocalDeclD `r stateTy fun r => Meta.withLocalDeclD `q probeTy fun q => do
            let e ← Meta.whnf (mkAppN obs #[r, q])
            let e ← Meta.reduce e (skipTypes := true) (skipProofs := true)
            let e := ((e.replaceFVars ps markers).replaceFVar r (.const `row [])).replaceFVar q (.const `probe [])
            pure (toSQL env 1000 e)
          let ansSQL := match ansTy.getAppFn.constName? with
            | some ``Nat => "integer" | some ``Bool => "boolean" | _ => "integer[]"
          return some s!"face {n.getString!} {stateName} Nat params={ps.size} ans={ansSQL} probe={arm}"
        let ctors : List Name ← if probeName == ``Unit then pure [``Unit.unit] else
          match env.find? probeName with
          | some (.inductInfo ii) => if isEnumName env probeName then pure ii.ctors else return none
          | _ => return none
        let obs ← Meta.mkProjection F `obs
        let markers := ps.mapIdx fun i _ => Expr.const (Name.mkSimple s!"p{i + 1}") []
        let arms : Array String ← Meta.withLocalDeclD `r stateTy fun r => do
          let mut arms : Array String := #[]
          for c in ctors do
            let e ← Meta.whnf (mkAppN obs #[r, .const c []])
            let e ← Meta.reduce e (skipTypes := true) (skipProofs := true)
            let e := (e.replaceFVars ps markers).replaceFVar r (.const `row [])
            arms := arms.push s!"{if c == ``Unit.unit then "unit" else c.getString!}={toSQL env 1000 e}"
          return arms
        let ansSQL := match ansTy.getAppFn.constName? with
          | some ``Nat => "integer" | some ``Bool => "boolean" | some ``List => "integer[]" | _ => "integer[]"
        return some s!"face {n.getString!} {stateName} {probeName} params={ps.size} ans={ansSQL} {" ".intercalate arms.toList}")
    if let some line := res.1 then IO.println line
  for (n, ci) in env.constants.map₂.toList do
    if n.getPrefix != sc.ns || !ci.isTheorem then continue
    let used := ci.type.getUsedConstants ++ (match ci.value? (allowOpaque := true) with | some v => v.getUsedConstants | none => #[])
    let seats := used.filter fun c => c.getPrefix == sc.ns && (env.find? c).any (fun ci' => !ci'.isTheorem && ci'.type.isAppOf ``List)
    if !seats.isEmpty then IO.println s!"cites {n.getString!} {" ".intercalate (seats.toList.map (·.getString!))}"
  -- the derived: a def over a table's row — a view (data), a constraint (prop), a clerk (state to
  -- state, a procedure) — with its body printed for the drawer's fragment, and the theorems that
  -- describe it (those whose statement names it)
  let mut derivedSeen : Array Name := #[]
  for (n, ci) in env.constants.map₂.toList ++ (vocab.toList.filterMap fun c => (env.find? c).map (c, ·)) do
    if !(sc.owns n) || ci.isTheorem || derivedSeen.contains n then continue
    derivedSeen := derivedSeen.push n
    let some v := ci.value? | continue
    let mut ty := ci.type
    let mut args : Array String := #[]
    while ty.isForall do
      args := args.push s!"{ty.bindingName!}:{ty.bindingDomain!}"
      ty := ty.bindingBody!
    if args.isEmpty then continue
    -- the printed type carries universe annotations (`List.{0} Roster.Party`); shed them
    let shed (t : String) : String := Id.run do
      let mut out := ""; let mut skip := false
      let cs := t.toList
      for i in [0:cs.length] do
        let c := cs[i]!
        if !skip && c == '.' && i + 1 < cs.length && cs[i+1]! == '{' then
          skip := true
        else if skip then
          if c == '}' then skip := false
        else
          out := out.push c
      return out
    let firstTy := shed ((args[0]!.splitOn ":").getD 1 "")
    -- the arguments after the row, as the shadow types them: a number an integer, a list or a
    -- tuple an array, a table an id, a Bool a boolean
    let sqlTy (t : String) : String :=
      let t := (shed t).trimAscii.toString
      if t == "Nat" then "integer" else if t == "Bool" then "boolean"
      else if t.startsWith "List " && isEnumName env (t.drop 5).trimAscii.toString.toName then enumSQL (t.drop 5).trimAscii.toString.toName ++ "[]"
      else if t.startsWith "List (List " || t.startsWith "List List " then "integer[][]"
      else if t.startsWith "List " then "integer[]"
      else if (t.splitOn "×").length > 1 then "integer[]"
      else if isEnumName env t.toName then enumSQL t.toName
      else if isStructure env t.toName then "integer" else "integer[]"
    let argtypesAll := ",".intercalate (args.toList.map fun a => sqlTy ((a.splitOn ":").getD 1 ""))
    let argtypes := ",".intercalate ((args.toList.drop 1).map fun a => sqlTy ((a.splitOn ":").getD 1 ""))
    let over := firstTy.splitOn " " |>.headD ""
    -- only defs whose first argument is a table (a structure of the shadow) or a list of one
    let listOf := (firstTy.startsWith "List ") && isStructure env ((firstTy.drop 5).trimAscii.toString.toName)
    let overName := if listOf then (firstTy.drop 5).trimAscii.toString.toName else over.toName
    let isTable := !listOf && isStructure env overName
    -- a def over an enum is a rule: read by reducing it at each constructor (one argument), or as
    -- its body over positional parameters (more)
    if isEnumName env overName then
      let retSQL := sqlTy s!"{ty}"
      let params := ",".intercalate (args.toList.map fun a => sqlTy ((a.splitOn ":").getD 1 ""))
      let some (.inductInfo ii) := env.find? overName | continue
      if args.size == 1 then
        let arms ← (Meta.MetaM.toIO (ctxCore := { fileName := trail, fileMap := default }) (sCore := { env := env }) do
          let mut arms : Array String := #[]
          for c in ii.ctors do
            let e ← Meta.whnf (mkAppN (.const n []) #[.const c []])
            let e ← Meta.reduce e (skipTypes := true) (skipProofs := true)
            arms := arms.push s!"WHEN '{c.getString!}' THEN {toSQL env 1000 e}"
          pure arms)
        IO.println s!"rule {n.getString!} depth=1 ret={retSQL} params={params} :: CASE $0 {" ".intercalate arms.1.toList} END"
      else
        let mut body := v
        let mut depth := 0
        while body.isLambda do body := body.bindingBody!; depth := depth + 1
        IO.println s!"rule {n.getString!} depth={depth} ret={retSQL} params={params} :: {toSQL env 1000 body}"
      continue
    -- a def every argument of which is a number, a truth, a list of numbers, or an enum is pure:
    -- a function of its arguments alone, no row
    let scalarTy (t : String) : Bool :=
      let t := (shed t).trimAscii.toString
      t == "Nat" || t == "Bool" || t == "List Nat" || isEnumName env t.toName
    let pure := !(isTable || listOf) && args.toList.all (fun a => scalarTy ((a.splitOn ":").getD 1 "")) && scalarTy s!"{ty}"
    if !(isTable || listOf || pure) then continue
    let kind := if pure then "pure" else if ty.isProp || ty.isConstOf ``Bool then "prop" else if ty.isSort then "sort" else if isTable && ty.getAppFn.constName? == some overName then "clerk" else "data"
    let mut body := v
    let mut depth := 0
    while body.isLambda do body := body.bindingBody!; depth := depth + 1
    -- the row is the first argument: de Bruijn index depth - 1 at the body
    let depth' := if depth == 0 then 1 else depth
    let sql := if kind == "pure" then toSQL env 1000 body
      else if kind == "clerk" && body.isAppOf `Face.park then toSQL env (depth' - 1) body
      else if kind == "clerk" then
        -- a constructor with fields: name each as `field = expr`, keeping only those not the row's own
        let args := body.getAppArgs
        let fields := getStructureFields env overName
        let sets := (List.range fields.size).filterMap fun i =>
          match args[i]?, fields[i]? with
          | some a, some f =>
            let same := match a with
              | .app (.const c _) (.bvar j) => c == overName ++ f && j == depth' - 1
              | .proj sn j (.bvar k) => sn == overName && j == i && k == depth' - 1
              | _ => false
            if same then none else some s!"{f} = {toSQL env (depth' - 1) a}"
          | _, _ => none
        "SET " ++ ", ".intercalate sets
      else toSQL env (depth' - 1) body
    let describers := (env.constants.map₂.toList.filter fun (m, ci') => m.getPrefix == sc.ns && ci'.isTheorem && ci'.type.getUsedConstants.contains n).map (·.1.getString!)
    let mut sql := sql
    let mut byThm := ""
    -- a def by structural recursion over its list whose step is `cond P V (self tail …)` is a
    -- FIRST MATCH: the first element the condition selects, read by V, else the base case — read
    -- from the def's own equations, the head as `x`, the list as the row
    if sql.startsWith "⟨unread" && (listOf || (pure && firstTy.startsWith "List ")) then
      let fm ← (Meta.MetaM.toIO (ctxCore := { fileName := trail, fileMap := default }) (sCore := { env := env }) do
        let eqns? ← Meta.getEqnsFor? n
        let some eqns := eqns? | do IO.eprintln s!"FIRST {n}: no eqns"; return (none : Option String)
        if eqns.size != 2 then do IO.eprintln s!"FIRST {n}: {eqns.size} eqns"; return (none : Option String)
        let env' ← Lean.getEnv
        let some e1 := env'.find? eqns[0]! | do IO.eprintln s!"FIRST {n}: refused at 1"; return (none : Option String)
        let some e2 := env'.find? eqns[1]! | do IO.eprintln s!"FIRST {n}: refused at 2"; return (none : Option String)
        let mut t1 := e1.type; let mut d1 := 0
        while t1.isForall do t1 := t1.bindingBody!; d1 := d1 + 1
        let mut t2 := e2.type; let mut d2 := 0
        while t2.isForall do t2 := t2.bindingBody!; d2 := d2 + 1
        if !(t1.isAppOfArity ``Eq 3 && t2.isAppOfArity ``Eq 3) then do IO.eprintln s!"FIRST {n}: refused at 3"; return (none : Option String)
        -- the base: `self [] params = D`, the step: `self (c :: cs) params = cond P V (self cs params)`
        if !((t1.getArg! 1).getAppArgs.any fun a => a.isAppOf ``List.nil) then do IO.eprintln s!"FIRST {n}: refused at 4"; return (none : Option String)
        let rhs2 := t2.getArg! 2
        if !(rhs2.isAppOfArity ``cond 4) then do IO.eprintln s!"FIRST {n}: refused at 5"; return (none : Option String)
        let recur := (rhs2.getAppArgs)[3]!
        if !(recur.isAppOf n) then do IO.eprintln s!"FIRST {n}: refused at 6"; return (none : Option String)
        -- the equations' binders are read off their own left sides: at the step, the list argument
        -- is `head :: tail` (the head reads as `x`, the tail must not appear) and each other argument
        -- is a binder that keeps the def's own index; at the base likewise
        let lhs2 := t2.getArg! 1
        let a2 := lhs2.getAppArgs
        let mut subst2 : Array Expr := Array.replicate d2 (Expr.const `tail [])
        for j in [0:a2.size] do
          let a := a2[j]!
          if j == 0 then
            if a.isAppOfArity ``List.cons 3 then
              match a.getArg! 1, a.getArg! 2 with
              | .bvar h, .bvar t => subst2 := (subst2.set! h (.const `x [])).set! t (.const `tail [])
              | _, _ => do IO.eprintln s!"FIRST {n}: the head or tail is not a binder"; return (none : Option String)
            else do IO.eprintln s!"FIRST {n}: the step's list is not a cons"; return (none : Option String)
          else match a with
            | .bvar p => subst2 := subst2.set! p (.bvar (depth' - 1 - j))
            | _ => do IO.eprintln s!"FIRST {n}: an argument at the step is not a binder"; return (none : Option String)
        let lhs1 := t1.getArg! 1
        let a1 := lhs1.getAppArgs
        let mut subst1 : Array Expr := Array.replicate d1 (Expr.const `tail [])
        for j in [0:a1.size] do
          if j == 0 then continue
          match a1[j]! with
          | .bvar p => subst1 := subst1.set! p (.bvar (depth' - 1 - j))
          | _ => do IO.eprintln s!"FIRST {n}: an argument at the base is not a binder"; return (none : Option String)
        let P := ((rhs2.getAppArgs)[1]!).instantiate subst2
        let V := ((rhs2.getAppArgs)[2]!).instantiate subst2
        let D := (t1.getArg! 2).instantiate subst1
        let ps := toSQL env 1000 P; let vs := toSQL env 1000 V; let ds := toSQL env 1000 D
        if ((ps ++ vs ++ ds).splitOn "⟨unread").length > 1 then do IO.eprintln s!"FIRST {n}: refused at 7"; return (none : Option String)
        if ((ps ++ vs).splitOn "tail").length > 1 then do IO.eprintln s!"FIRST {n}: refused at 8"; return (none : Option String)
        return some s!"coalesce((SELECT {vs} FROM unnest(row) WITH ORDINALITY AS u(x, o) WHERE {ps} ORDER BY o LIMIT 1), {ds})")
      if let some f := fm.1 then sql := f
    -- a def the fragment cannot read may be read BY A THEOREM: `∀ x, g x = n x` (or `n x = g x`)
    -- with g readable — the shadow draws from the proof, and names it
    if sql.startsWith "⟨unread" then
      for (m, ci') in env.constants.toList do
        if !(sc.owns m) || !ci'.isTheorem then continue
        let mut t := ci'.type
        let mut d := 0
        while t.isForall do t := t.bindingBody!; d := d + 1
        -- the theorem's binders are the def's parameters in order: the row is the outermost, so its
        -- de Bruijn index at the equation is d - 1, exactly as at the def's own body
        if d == depth' && t.isAppOfArity ``Eq 3 then
          let l := t.getArg! 1; let r := t.getArg! 2
          let inOrder (e : Expr) := e.getAppArgs.toList == (List.range d).reverse.map Expr.bvar
          let isSelf (e : Expr) := e.isApp && e.getAppFn.constName? == some n && inOrder e
          let mentions (e : Expr) := e.getUsedConstants.contains n
          let other := if isSelf r && !mentions l then some l else if isSelf l && !mentions r then some r else none
          if let some o := other then
            let s' := toSQL env (d - 1) o
            if !(s'.startsWith "⟨unread") && !((s'.splitOn "⟨unread").length > 1) then
              sql := s'; byThm := m.getString!; break
    IO.println s!"derived {n.getString!} {kind} over={firstTy} args={depth'} argtypes={if kind == "pure" then argtypesAll else argtypes} ret={sqlTy s!"{ty}"} :: {sql} | {" ".intercalate describers}{if byThm.isEmpty then "" else s!" @by {byThm}"}"


unsafe def main (args : List String) : IO Unit := do
  Lean.enableInitializersExecution
  Lean.initSearchPath (← Lean.findSysroot)
  let sc := Scope.parse args 2
  if args.head? == some "reasons" then
    reasonsMode args[1]! sc
    return
  if args.head? == some "roots" then
    -- by computation (default transparency: the house's own notion, a citation that closes by
    -- unfolding is an instance) or by shape (reducible: the statements as written)
    rootsMode args[1]! sc (args.contains "shape")
    return
  if args.head? == some "census" then
    censusMode args[1]! sc
    return
  if args.head? == some "kinds" then
    kindsMode args[1]! sc
    return
  if args.head? == some "schema" then
    schemaMode args[1]! sc
    return
  IO.eprintln "readings: reasons | roots [shape] | census | kinds | schema  <grown stream> <ns> [imports]"
